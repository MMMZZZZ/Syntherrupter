Page Help_Info
    Attributes
        ID               : 0
        Scope            : local
        Send Component ID: disabled
        Locked           : no
    
    Events
        Preinitialize Event
            // Prevent page refreshs until postinit
            ref_stop
            //
            // Load colors
            click fLoadColors,1
            click fLoadModes,1
            //
            // Display username
            if(User_Settings.userNum.val==0)
            {
                tUser.txt=" "+User_Settings.u0Name.txt
            }else if(User_Settings.userNum.val==1)
            {
                tUser.txt=" "+User_Settings.u1Name.txt
            }else if(User_Settings.userNum.val==2)
            {
                tUser.txt=" "+User_Settings.u2Name.txt
            }
            //
            vis fLoadModes,0
            vis fLoadColors,0
            // Load Help Enable Button
            btHelp.val=helpMode.val
            click btHelp,0
            //
            // Load Help Mode
            if(Help_Info.helpMode.val==0)
            {
                vis qrHelp,0
            }else
            {
                tempStr.txt=tTitle.txt
                showHelp.val=1
                click 0,1
            }
            //
            // Enable page refreshs again
            ref_star
        
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(Other_Settings.nBackOff.val!=0)
                {
                    // Default "stop" action.
                    // Set ontime and duty to 0 for all modes. See fSysex for details
                    // Disable all modes as well.
                    printh f0 00 26 06 01 7f 20 00 00 7f 00 00 00 00 00 f7
                    printh f0 00 26 05 01 7f 21 00 00 7f 00 00 00 00 00 f7
                    printh f0 00 26 05 01 7f 22 00 00 7f 00 00 00 00 00 f7
                    // Set all ontime and duty settings of all pages to 0.
                    // First come the global variables then the local variables.
                    Simple.ontime.val=0
                    Simple.duty.val=0
                    Simple.set1.val=Simple.set1.val&0x0000ffff
                    Simple.set2.val=Simple.set2.val&0x0000ffff
                    Simple.set3.val=Simple.set3.val&0x0000ffff
                    Simple.set4.val=Simple.set4.val&0x0000ffff
                    Simple.set5.val=Simple.set5.val&0x0000ffff
                    Simple.set6.val=Simple.set6.val&0x0000ffff
                    MIDI_Live.ontime.val=0
                    MIDI_Live.duty.val=0
                    MIDI_Live.set1.val=0
                    MIDI_Live.set2.val=0
                    MIDI_Live.set3.val=0
                    MIDI_Live.set4.val=0
                    MIDI_Live.set5.val=0
                    MIDI_Live.set6.val=0
                    Lightsaber.ontime.val=0
                    Lightsaber.ontimes12.val=0
                    Lightsaber.ontimes34.val=0
                    Lightsaber.ontimes56.val=0
                }
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkf2f"
                click qrHelp,0
            }
        
Variable (int32) helpMode
    Attributes
        ID   : 12
        Scope: global
        Value: 0
    
Variable (string) tempStr
    Attributes
        ID            : 13
        Scope         : local
        Text          : 
        Max. Text Size: 32
    
Variable (int32) showHelp
    Attributes
        ID   : 14
        Scope: local
        Value: 0
    
Text tTitle
    Attributes
        ID                 : 1
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Help and Information
        Max. Text Size     : 22
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tUser
    Attributes
        ID                 : 2
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               :  Guest
        Max. Text Size     : 32
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMSI
    Attributes
        ID                 : 3
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Simple
        Max. Text Size     : 16
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMML
    Attributes
        ID                 : 4
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : MIDI Live
        Max. Text Size     : 16
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMLS
    Attributes
        ID                 : 5
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Lightsaber
        Max. Text Size     : 16
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tButtons
    Attributes
        ID                 : 6
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Help Mode:
        Max. Text Size     : 40
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tCredits
    Attributes
        ID                 : 7
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Credits
        Max. Text Size     : 40
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tChnInfo
    Attributes
        ID                 : 8
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : With Help Mode enabled, a QR code appears at the top right corner. It brings you immediately \r\nto the Syntherrupter Wiki section for this page. If you want help for a specific component, \r\npress the QR code (you'll notice the page title change), then press the component you want\r\nhelp for. The QR code now points to the specific section for this component in the wiki.
        Max. Text Size     : 400
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tCreditsInfo
    Attributes
        ID                 : 9
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Forums with great people: High Voltage Forum, Unofficial Nextion Forum.\r\nSpecial thanks to: Jens Kerrines (a.k.a. Netzpfuscher)\r\nMore info on github.com/MMMZZZZ/Syntherrupter#credits (Use Help Mode to get QR code)\r\n\r\nDevelopped by Max Zuidberg
        Max. Text Size     : 300
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                // Make this object "transparent" for clicks and click background.
                click 0,1
            }else
            {
                // Show Help
                // Undo button toggle
                btHelp.val=1-btHelp.val
                qrHelp.txt="git.io/JLxS0"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
QR Code qrHelp
    Attributes
        ID               : 16
        Scope            : local
        Send Component ID: disabled
        Text             : git.io/JkvOK
        Max. Text Size   : 16
    
    Events
        Touch Release Event
            if(showHelp.val==0)
            {
                showHelp.val=1
                tempStr.txt=tTitle.txt
                tTitle.txt="Get Help/Link for..."
                // keep QR code on top
                ref qrHelp
                // Show modified title before stopping screen refreshs.
                doevents
                ref_stop
            }else
            {
                showHelp.val=0
                tTitle.txt=tempStr.txt
                // keep QR code on top
                ref qrHelp
                ref_star
            }
        
Button bReturn
    Attributes
        ID               : 10
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : â†
        Max. Text Size   : 3
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkf2f"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                page Menu
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Dual-state Button btHelp
    Attributes
        ID               : 11
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : Disabled
        Max. Text Size   : 8
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                // Undo button toggle
                btHelp.val=1-btHelp.val
                qrHelp.txt="git.io/JLxSR"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(btHelp.val==0)
                {
                    btHelp.txt="Disabled"
                }else
                {
                    btHelp.txt="Enabled"
                }
                helpMode.val=btHelp.val
                vis qrHelp,helpMode.val
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Hotspot fLoadModes
    Attributes
        ID               : 15
        Scope            : local
        Send Component ID: disabled
    
    Events
        Touch Press Event
            // Load active modes
            sys0=Settings.activeModes.val&4
            if(sys0!=0)
            {
                // Lightsaber Mode is active
                tMLS.bco=Settings.backSelectCol2.val
            }
            //
            // Display all active modes
            sys0=Settings.activeModes.val&1
            if(sys0!=0)
            {
                // Simple Mode is active
                tMSI.bco=Settings.backSelectCol2.val
            }
            sys0=Settings.activeModes.val&2
            if(sys0!=0)
            {
                // MIDI Live Mode is active
                tMML.bco=Settings.backSelectCol2.val
            }
        
Hotspot fLoadColors
    Attributes
        ID               : 17
        Scope            : local
        Send Component ID: disabled
    
    Events
        Touch Press Event
            // Changes all page components according to the current color set.
            // Note: This component has to be the last component of the page.
            //
            // sys0: component id
            // sys1: component type
            // sys2: picture id offset for current color mode
            sys2=Settings.colorMode.val*Settings.picCount.val
            for(sys0=0;sys0<fLoadColors.id;sys0++)
            {
                // More convenient for typing
                sys1=b[sys0].type
                if(sys1==121)
                {
                    // Types that have bco
                    // page
                    if(b[sys0].bco==Settings.backCol2Deflt.val)
                    {
                        b[sys0].bco=Settings.backCol2.val
                    }else
                    {
                        b[sys0].bco=Settings.backCol.val
                    }
                }else if(sys1==54||sys1==59||sys1==116||sys1==55)
                {
                    // Types that have bco, pco, and sta:
                    // number, float, text, scroll text
                    if(b[sys0].sta==1)
                    {
                        if(b[sys0].bco==Settings.backCol2Deflt.val)
                        {
                            b[sys0].bco=Settings.backCol2.val
                            b[sys0].pco=Settings.frontCol2.val
                        }else
                        {
                            b[sys0].bco=Settings.backCol.val
                            b[sys0].pco=Settings.frontCol.val
                        }
                    }
                }else if(sys1==98||sys1==53)
                {
                    // Types that have pco, pic, pco2, pic2, sta:
                    // button, dual-state button
                    if(b[sys0].sta==2)
                    {
                        b[sys0].pco=Settings.frontCol.val
                        b[sys0].pco2=Settings.backCol.val
                        b[sys0].pic=b[sys0].pic%Settings.picCount.val
                        b[sys0].pic+=sys2
                        b[sys0].pic2=b[sys0].pic+1
                    }
                }else if(sys1==58)
                {
                    // Types that have bco, pco
                    // QR Code
                    b[sys0].bco=Settings.backCol.val
                    b[sys0].pco=Settings.qrFrontCol.val
                }else if(sys1==1)
                {
                    // Types that have pic, pic1, pic2:
                    // slider
                    b[sys0].pic=b[sys0].pic%Settings.picCount.val
                    b[sys0].pic+=sys2
                    b[sys0].pic1=b[sys0].pic
                    b[sys0].pic2=b[sys0].pic2%Settings.picCount.val
                    b[sys0].pic2+=sys2
                }
            }
        
