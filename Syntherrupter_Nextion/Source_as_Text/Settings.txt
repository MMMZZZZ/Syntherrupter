Page Settings
    Events
        Preinitialize Event
            // Prevent page refreshs until postinit
            ref_stop
            //
            // Load colors
            click fLoadColors,1
            //
            // Display username
            if(User_Settings.userNum.val==0)
            {
                tUser.txt=" "+User_Settings.u0Name.txt
            }else if(User_Settings.userNum.val==1)
            {
                tUser.txt=" "+User_Settings.u1Name.txt
            }else if(User_Settings.userNum.val==2)
            {
                tUser.txt=" "+User_Settings.u2Name.txt
            }
            sys0=Settings.activeModes.val&4
            if(sys0!=0)
            {
                // Lightsaber Mode is active
                tMLS.bco=6900
            }
            //
            // Display all active modes
            sys0=Settings.activeModes.val&1
            if(sys0!=0)
            {
                // Simple Mode is active
                tMSI.bco=6900
            }
            sys0=Settings.activeModes.val&2
            if(sys0!=0)
            {
                // MIDI Live Mode is active
                tMML.bco=6900
            }
            // Not all users have the same rights
            if(User_Settings.userNum.val!=2)
            {
                vis bCoilSettings,0
                vis bNXTUpdate,0
                vis bESPUpdate,0
            }
            // Tell the microcontroller: Mode: SEttings
            prints "mse",0
            //
            // Enable page refreshs again
            ref_star
        
        Postinitialize Event
            
        Touch Press Event
            if(Other_Settings.nBackOff.val!=0)
            {
                // Default "stop" action.
                // Send Mode Emergency Stop which turns all outputs off (but actually keeps the current mode)
                prints "mes",3
                // Set all ontime and duty settings of all pages to 0.
                // First come the global variables then the local variables.
                Simple.ontime.val=0
                Simple.duty.val=0
                Simple.set1.val=Simple.set1.val&0x0000ffff
                Simple.set2.val=Simple.set2.val&0x0000ffff
                Simple.set3.val=Simple.set3.val&0x0000ffff
                Simple.set4.val=Simple.set4.val&0x0000ffff
                Simple.set5.val=Simple.set5.val&0x0000ffff
                Simple.set6.val=Simple.set6.val&0x0000ffff
                MIDI_Live.ontime.val=0
                MIDI_Live.duty.val=0
                MIDI_Live.set1.val=0
                MIDI_Live.set2.val=0
                MIDI_Live.set3.val=0
                MIDI_Live.set4.val=0
                MIDI_Live.set5.val=0
                MIDI_Live.set6.val=0
                Lightsaber.ontime.val=0
                Lightsaber.ontimes12.val=0
                Lightsaber.ontimes34.val=0
                Lightsaber.ontimes56.val=0
            }
        
        Touch Release Event
            
        Page Exit Event
            
    Components
        Button bCoilSettings
            Properties
                Scope      : Local
                Caption    : "Coil Limits"
                Max. length: 20
            
            Events
                Touch Press Event
                    
                Touch Release Event
                    page TC_Settings
                
        Text tTitle
            Properties
                Scope       : Local
                Initial Text: "Settings"
                Max. length : 22
            
            Events
                Touch Press Event
                    
                Touch Release Event
                    
        Button bReturn
            Properties
                Scope      : Local
                Caption    : "Save"
                Max. length: 10
            
            Events
                Touch Press Event
                    
                Touch Release Event
                    // Mode: EXit.
                    prints "mex",0
                    page Menu
                
        Button bNXTUpdate
            Properties
                Scope      : Local
                Caption    : "Nextion<->USB"
                Max. length: 20
            
            Events
                Touch Press Event
                    
                Touch Release Event
                    page USB_NXT_Pass
                
        Button bGeneral
            Properties
                Scope      : Local
                Caption    : "General"
                Max. length: 20
            
            Events
                Touch Press Event
                    
                Touch Release Event
                    page Other_Settings
                
        Button bUsers
            Properties
                Scope      : Local
                Caption    : "Users"
                Max. length: 20
            
            Events
                Touch Press Event
                    
                Touch Release Event
                    page User_Settings
                
        Variable activeModes
            Properties
                Scope         : Global
                Type          : int32
                Initial Value : 0
            
        Text tUser
            Properties
                Scope       : Local
                Initial Text: " Guest"
                Max. length : 32
            
            Events
                Touch Press Event
                    // Make this object "transparent" for clicks and click background.
                    click 0,1
                
                Touch Release Event
                    
        Text tMSI
            Properties
                Scope       : Local
                Initial Text: "Simple"
                Max. length : 16
            
            Events
                Touch Press Event
                    // Make this object "transparent" for clicks and click background.
                    click 0,1
                
                Touch Release Event
                    
        Text tMML
            Properties
                Scope       : Local
                Initial Text: "MIDI Live"
                Max. length : 16
            
            Events
                Touch Press Event
                    // Make this object "transparent" for clicks and click background.
                    click 0,1
                
                Touch Release Event
                    
        Text tMLS
            Properties
                Scope       : Local
                Initial Text: "Lightsaber"
                Max. length : 16
            
            Events
                Touch Press Event
                    // Make this object "transparent" for clicks and click background.
                    click 0,1
                
                Touch Release Event
                    
        Button bEnvelope
            Properties
                Scope      : Local
                Caption    : "Envelopes"
                Max. length: 20
            
            Events
                Touch Press Event
                    
                Touch Release Event
                    page Env_Settings
                
        Button bESPUpdate
            Properties
                Scope      : Local
                Caption    : "ESP<->USB"
                Max. length: 20
            
            Events
                Touch Press Event
                    
                Touch Release Event
                    page USB_ESP_Pass
                
        Variable backCol
            Properties
                Scope         : Global
                Type          : int32
                Initial Value : 65535
            
        Variable frontCol
            Properties
                Scope         : Global
                Type          : int32
                Initial Value : 6900
            
        Variable frontCol2
            Properties
                Scope         : Global
                Type          : int32
                Initial Value : 65535
            
        Variable picCount
            Properties
                Scope         : Global
                Type          : int32
                Initial Value : 31
            
        Variable colorMode
            Properties
                Scope         : Global
                Type          : int32
                Initial Value : 0
            
        Hotspot fLoadColors
            Events
                Touch Press Event
                    // Changes all page components according to the current color set.
                    // Note: This component has to be the last component of the page.
                    //
                    // sys0: component id
                    // sys1: component type
                    // sys2: picture id offset for current color mode
                    sys2=Settings.colorMode.val*Settings.picCount.val
                    for(sys0=0;sys0<fLoadColors.id;sys0++)
                    {
                        // More convenient for typing
                        sys1=b[sys0].type
                        if(sys1==121)
                        {
                            // Types that have bco
                            // page
                            if(b[sys0].bco!=Settings.backCol2.val)
                            {
                                b[sys0].bco=Settings.backCol.val
                            }
                        }else if(sys1==54||sys1==59||sys1==116||sys1==55)
                        {
                            // Types that have bco, pco, and sta:
                            // number, float, text, scroll text
                            if(b[sys0].sta==1)
                            {
                                if(b[sys0].bco!=Settings.backCol2.val)
                                {
                                    b[sys0].bco=Settings.backCol.val
                                    b[sys0].pco=Settings.frontCol.val
                                }else
                                {
                                    b[sys0].pco=Settings.frontCol2.val
                                }
                            }
                        }else if(sys1==98||sys1==53)
                        {
                            // Types that have pco, pic, pco2, pic2, sta:
                            // button, dual-state button
                            if(b[sys0].sta==2)
                            {
                                b[sys0].pco=Settings.frontCol.val
                                b[sys0].pco2=Settings.backCol.val
                                b[sys0].pic=b[sys0].pic%Settings.picCount.val
                                b[sys0].pic+=sys2
                                b[sys0].pic2=b[sys0].pic+1
                            }
                        }else if(sys1==1)
                        {
                            // Types that have pic, pic2:
                            // slider
                            b[sys0].pic=b[sys0].pic%Settings.picCount.val
                            b[sys0].pic+=sys2
                            b[sys0].pic2=b[sys0].pic2%Settings.picCount.val
                            b[sys0].pic2+=sys2
                        }
                    }
                
                Touch Release Event
                    
        Variable backCol2
            Properties
                Scope         : Global
                Type          : int32
                Initial Value : 4589
            
