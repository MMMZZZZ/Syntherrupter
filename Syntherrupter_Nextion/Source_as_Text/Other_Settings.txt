Page Other_Settings
    Attributes
        ID               : 0
        Scope            : local
        Send Component ID: disabled
        Locked           : no
    
    Events
        Preinitialize Event
            // Prevent page refreshs until postinit
            ref_stop
            //
            // Load colors
            click fLoadColors,1
            click fLoadModes,1
            //
            // Display username
            if(User_Settings.userNum.val==0)
            {
                tUser.txt=" "+User_Settings.u0Name.txt
            }else if(User_Settings.userNum.val==1)
            {
                tUser.txt=" "+User_Settings.u1Name.txt
            }else if(User_Settings.userNum.val==2)
            {
                tUser.txt=" "+User_Settings.u2Name.txt
            }
            //
            vis fLoadModes,0
            vis fLoadColors,0
            vis idle,0
            vis fCheckNApply,0
            vis fSqrt,0
            //
            newValTargetId.val=idle.id
            // Set dark mode button state
            btDark.val=Settings.colorMode.val
            //
            // Load settings from global variables.
            fSqrt.val=dim*sBrightness.maxval
            click fSqrt,1
            sBrightness.val=fSqrt.val
            sys0=thsp
            nSleepMin.val=sys0/60
            sys1=nSleepMin.val*60
            sys0-=sys1
            nSleepS.val=sys0
            //
            // Load Help Mode
            if(Help_Info.helpMode.val==0)
            {
                vis qrHelp,0
            }else
            {
                vis qrHelp,1
                tempStr.txt=tTitle.txt
                showHelp.val=1
                click 0,1
            }
            //
            // Enable page refreshs again
            ref_star
        
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(Other_Settings.nBackOff.val!=0)
                {
                    // Default "stop" action.
                    // Set ontime and duty to 0 for all modes. See fSysex for details
                    // Disable all modes as well.
                    printh f0 00 26 06 01 7f 20 00 00 7f 00 00 00 00 00 f7
                    printh f0 00 26 05 01 7f 21 00 00 7f 00 00 00 00 00 f7
                    printh f0 00 26 05 01 7f 22 00 00 7f 00 00 00 00 00 f7
                    // Set all ontime and duty settings of all pages to 0.
                    // First come the global variables then the local variables.
                    Simple.ontime.val=0
                    Simple.duty.val=0
                    Simple.set1.val=Simple.set1.val&0x0000ffff
                    Simple.set2.val=Simple.set2.val&0x0000ffff
                    Simple.set3.val=Simple.set3.val&0x0000ffff
                    Simple.set4.val=Simple.set4.val&0x0000ffff
                    Simple.set5.val=Simple.set5.val&0x0000ffff
                    Simple.set6.val=Simple.set6.val&0x0000ffff
                    MIDI_Live.ontime.val=0
                    MIDI_Live.duty.val=0
                    MIDI_Live.set1.val=0
                    MIDI_Live.set2.val=0
                    MIDI_Live.set3.val=0
                    MIDI_Live.set4.val=0
                    MIDI_Live.set5.val=0
                    MIDI_Live.set6.val=0
                    Lightsaber.ontime.val=0
                    Lightsaber.ontimes12.val=0
                    Lightsaber.ontimes34.val=0
                    Lightsaber.ontimes56.val=0
                }
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
                click qrHelp,0
            }
        
Variable (int32) newValTargetId
    Attributes
        ID   : 6
        Scope: local
        Value: 20
    
Variable (int32) newSelect
    Attributes
        ID   : 7
        Scope: local
        Value: 0
    
Variable (string) tempStr
    Attributes
        ID            : 39
        Scope         : local
        Text          : 
        Max. Text Size: 32
    
Variable (int32) showHelp
    Attributes
        ID   : 40
        Scope: local
        Value: 0
    
Number nHoldTime
    Attributes
        ID                 : 5
        Scope              : global
        Send Component ID  : disabled
        Associated Keyboard: none
        Value              : 250
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                // Check if values are legal and if so, apply them
                click fCheckNApply,0
                // Reset push state of last selected value
                b[newValTargetId.val].bco=idle.bco
                b[newValTargetId.val].pco=idle.pco
                // Mark this field as pushed
                nHoldTime.bco=idle.pco
                nHoldTime.pco=idle.bco
                newValTargetId.val=nHoldTime.id
                // A new value has been selected
                newSelect.val=1
            }else
            {
                // Show Help
                qrHelp.txt="git.io/JLxSn"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Number nSleepMin
    Attributes
        ID                 : 25
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Value              : 5
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                // Check if values are legal and if so, apply them
                click fCheckNApply,0
                // Reset push state of last selected value
                b[newValTargetId.val].bco=idle.bco
                b[newValTargetId.val].pco=idle.pco
                // Mark this field as pushed
                nSleepMin.bco=idle.pco
                nSleepMin.pco=idle.bco
                newValTargetId.val=nSleepMin.id
                // A new value has been selected
                newSelect.val=1
            }else
            {
                // Show Help
                qrHelp.txt="git.io/JLxSW"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Number nSleepS
    Attributes
        ID                 : 26
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Value              : 30
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                // Check if values are legal and if so, apply them
                click fCheckNApply,0
                // Reset push state of last selected value
                b[newValTargetId.val].bco=idle.bco
                b[newValTargetId.val].pco=idle.pco
                // Mark this field as pushed
                nSleepS.bco=idle.pco
                nSleepS.pco=idle.bco
                newValTargetId.val=nSleepS.id
                // A new value has been selected
                newSelect.val=1
            }else
            {
                // Show Help
                qrHelp.txt="git.io/JLxSW"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Number fSqrt
    Attributes
        ID                 : 30
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Value              : 0
    
    Events
        Touch Press Event
            // fXxxx are functions. Since they need to be clicked to execute their code they need to be on the same page. Usage:
            // 1. set the component value (fXxx.val) to the input value
            // 2. Execute code with "click fXxxx,1".
            // 3. Read the result from the component value (fXxx.val)
            // This functions calculates the square root.
            if(fSqrt.val>=2)
            {
                sys0=10
                sys2=fSqrt.val+1
                while(sys2>fSqrt.val)
                {
                    sys1=fSqrt.val/sys0
                    sys0+=sys1
                    sys0/=2
                    sys2=sys0*sys0
                }
                fSqrt.val=sys0
            }
        
Number nBackOff
    Attributes
        ID                 : 32
        Scope              : global
        Send Component ID  : disabled
        Associated Keyboard: none
        Value              : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                // Check if values are legal and if so, apply them
                click fCheckNApply,0
                // Reset push state of last selected value
                b[newValTargetId.val].bco=idle.bco
                b[newValTargetId.val].pco=idle.pco
                // Mark this field as pushed
                nBackOff.bco=idle.pco
                nBackOff.pco=idle.bco
                newValTargetId.val=nBackOff.id
                // A new value has been selected
                newSelect.val=1
            }else
            {
                // Show Help
                qrHelp.txt="git.io/JLxSl"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
XFloat idle
    Attributes
        ID                 : 8
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Value              : 0
    
Text tHideBack
    Attributes
        ID                 : 1
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               :  
        Max. Text Size     : 1
    
    Events
        Touch Press Event
            // This is just to hide the background around the sliders so that you do not turn the outputs off by accident.
        
Text tTitle
    Attributes
        ID                 : 2
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : General Settings
        Max. Text Size     : 22
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tHoldTime
    Attributes
        ID                 : 4
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Button hold Time:
        Max. Text Size     : 30
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tBrightness
    Attributes
        ID                 : 21
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Display brightness:
        Max. Text Size     : 30
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text fCheckNApply
    Attributes
        ID                 : 23
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               :  
        Max. Text Size     : 10
    
    Events
        Touch Release Event
            // Settings must stay in certain ranges. this function checks and fixes them.Afterwards settings are applied
            if(nBackOff.val!=0)
            {
                nBackOff.val=1
            }
            if(nHoldTime.val<50)
            {
                // Hold time must at least be 50ms. This is the lower limit of the nextion timer object
                nHoldTime.val=50
            }else if(nHoldTime.val>9999)
            {
                // Who would want to push a button 10s or more??
                nHoldTime.val=9999
            }
            if(nSleepMin.val>60)
            {
                nSleepMin.val=60
            }
            if(nSleepS.val>59)
            {
                nSleepS.val=59
            }
            thsp=nSleepMin.val*60+nSleepS.val
            click fSysex,1
        
Text tSleep
    Attributes
        ID                 : 24
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Enter sleep after:
        Max. Text Size     : 30
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMin
    Attributes
        ID                 : 27
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : min
        Max. Text Size     : 3
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tS
    Attributes
        ID                 : 28
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : s
        Max. Text Size     : 3
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tms
    Attributes
        ID                 : 29
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : ms
        Max. Text Size     : 2
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tBackOff
    Attributes
        ID                 : 31
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Background shutdown:
        Max. Text Size     : 30
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tUser
    Attributes
        ID                 : 33
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               :  Guest
        Max. Text Size     : 32
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMSI
    Attributes
        ID                 : 34
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Simple
        Max. Text Size     : 16
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMML
    Attributes
        ID                 : 35
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : MIDI Live
        Max. Text Size     : 16
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMLS
    Attributes
        ID                 : 36
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Lightsaber
        Max. Text Size     : 16
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tSelToMod
    Attributes
        ID                 : 38
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Select any value to modify.
        Max. Text Size     : 30
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
QR Code qrHelp
    Attributes
        ID               : 42
        Scope            : local
        Send Component ID: disabled
        Text             : git.io/JkvOK
        Max. Text Size   : 16
    
    Events
        Touch Release Event
            if(showHelp.val==0)
            {
                showHelp.val=1
                tempStr.txt=tTitle.txt
                tTitle.txt="Get Help/Link for..."
                // keep QR code on top
                ref qrHelp
                // Show modified title before stopping screen refreshs.
                doevents
                ref_stop
            }else
            {
                showHelp.val=0
                tTitle.txt=tempStr.txt
                // keep QR code on top
                ref qrHelp
                ref_star
            }
        
Slider sBrightness
    Attributes
        ID               : 22
        Scope            : local
        Send Component ID: disabled
        Position         : 100
        Upper range limit: 100
        Lower range limit: 10
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                // Check if values are legal and if so, apply them
                click fCheckNApply,0
                // Reset push state of last selected value
                b[newValTargetId.val].bco=idle.bco
                b[newValTargetId.val].pco=idle.pco
                // No value selected to modify
                newValTargetId.val=idle.id
                // A new value has been selected
                newSelect.val=0
            }else
            {
                // Show Help
                qrHelp.txt="git.io/JLxSC"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                sys0=sBrightness.val*sBrightness.val
                sys0/=sBrightness.maxval
                dim=sys0
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button bReturn
    Attributes
        ID               : 3
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : â†
        Max. Text Size   : 10
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                click bOK,0
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                page Settings
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b0
    Attributes
        ID               : 9
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 0
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b1
    Attributes
        ID               : 10
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 1
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
                b[newValTargetId.val].val+=1
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b2
    Attributes
        ID               : 11
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 2
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
                b[newValTargetId.val].val+=2
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b3
    Attributes
        ID               : 12
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 3
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
                b[newValTargetId.val].val+=3
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b4
    Attributes
        ID               : 13
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 4
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
                b[newValTargetId.val].val+=4
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b5
    Attributes
        ID               : 14
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 5
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
                b[newValTargetId.val].val+=5
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b6
    Attributes
        ID               : 15
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 6
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
                b[newValTargetId.val].val+=6
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b7
    Attributes
        ID               : 16
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 7
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
                b[newValTargetId.val].val+=7
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b8
    Attributes
        ID               : 17
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 8
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
                b[newValTargetId.val].val+=8
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button b9
    Attributes
        ID               : 18
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : 9
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(newSelect.val!=0)
                {
                    newSelect.val=0
                    b[newValTargetId.val].val=0
                }else
                {
                    b[newValTargetId.val].val*=10
                }
                b[newValTargetId.val].val+=9
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button bClear
    Attributes
        ID               : 19
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : X
        Max. Text Size   : 1
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                b[newValTargetId.val].val=0
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button bOK
    Attributes
        ID               : 20
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : ok
        Max. Text Size   : 2
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkfgj"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                b[newValTargetId.val].bco=idle.bco
                b[newValTargetId.val].pco=idle.pco
                newValTargetId.val=idle.id
                click fCheckNApply,0
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Dual-state Button btDark
    Attributes
        ID               : 37
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : Dark Mode
        Max. Text Size   : 20
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
            }else
            {
                // Show Help
                // Undo button toggle
                btDark.val=1-btDark.val
                qrHelp.txt="git.io/JLxS8"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                // REMEMBER TO CHANGE Startup.comOK!
                if(Settings.colorMode.val==0)
                {
                    Settings.colorMode.val=1
                    Settings.backCol.val=8484
                    Settings.backCol2.val=2145
                    Settings.backSelectCol2.val=16904
                    Settings.frontCol.val=40476
                    Settings.frontCol2.val=40476
                    Settings.qrFrontCol.val=40476
                }else
                {
                    Settings.colorMode.val=0
                    Settings.backCol.val=65535
                    Settings.backCol2.val=4589
                    Settings.backSelectCol2.val=6900
                    Settings.frontCol.val=6900
                    Settings.frontCol2.val=65535
                    Settings.qrFrontCol.val=6900
                }
                page dp
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Hotspot fLoadModes
    Attributes
        ID               : 41
        Scope            : local
        Send Component ID: disabled
    
    Events
        Touch Press Event
            // Load active modes
            sys0=Settings.activeModes.val&4
            if(sys0!=0)
            {
                // Lightsaber Mode is active
                tMLS.bco=Settings.backSelectCol2.val
            }
            //
            // Display all active modes
            sys0=Settings.activeModes.val&1
            if(sys0!=0)
            {
                // Simple Mode is active
                tMSI.bco=Settings.backSelectCol2.val
            }
            sys0=Settings.activeModes.val&2
            if(sys0!=0)
            {
                // MIDI Live Mode is active
                tMML.bco=Settings.backSelectCol2.val
            }
        
Hotspot fSysex
    Attributes
        ID               : 43
        Scope            : local
        Send Component ID: disabled
    
    Events
        Touch Press Event
            // Sends a Syntherrupter Sysex command that updates Syntherrupter with whatever
            // has been changed on this page.
            // Package format:
            // F0 00 DMID DMID VERSION DEVICE PN_LSB PN_MSB TG_LSB TG_MSB VAL_LSB VAL VAL VAL VAL_MSB F7
            // DMID DMID = 26 05
            // VERSION = 01
            // DEVICE = 7f (wildcard)
            // PN, TG, VAL: see syntherrupter sysex documentation
            //
            //
            // Display Brightness
            // SYSEX_START DMID VERSION DEVICE
            printh f0 00 26 05 01 7f
            // PN_LSB PN_MSB
            printh 20 02 // Display Brightness
            // TG_LSB
            printh 00 // Reserved
            // TG_MSB
            printh 00 // Reserved
            // VAL (4 bytes to be packed into 5 groups of 7 bit)
            // Brightness: 0-100
            prints dim,0
            // Brightness uses less than 7 bits, hardcode remaining bytes.
            printh 00 00 00 00
            // SYSEX_END
            printh f7
            //
            // Standby
            // SYSEX_START DMID VERSION DEVICE
            printh f0 00 26 05 01 7f
            // PN_LSB PN_MSB
            printh 21 02 // Standby
            // TG_LSB
            printh 00 // Reserved
            // TG_MSB
            printh 00 // Reserved
            // VAL (4 bytes to be packed into 5 groups of 7 bit)
            // Seconds til standby, 1-3600
            sys0=thsp
            sys2=sys0
            sys2&=0x7f
            prints sys2,1
            sys0>>=7
            sys2=sys0
            sys2&=0x7f
            prints sys2,1
            // Standby uses less than 14 bits, hardcode remaining bytes.
            printh 00 00 00
            // SYSEX_END
            printh f7
            //
            // Button Hold Time
            // SYSEX_START DMID VERSION DEVICE
            printh f0 00 26 05 01 7f
            // PN_LSB PN_MSB
            printh 22 02 // Button Hold Time
            // TG_LSB
            printh 00 // Reserved
            // TG_MSB
            printh 00 // Reserved
            // VAL (4 bytes to be packed into 5 groups of 7 bit)
            // Time in ms, 50-9999
            sys0=nHoldTime.val
            sys2=sys0
            sys2&=0x7f
            prints sys2,1
            sys0>>=7
            sys2=sys0
            sys2&=0x7f
            prints sys2,1
            // Standby uses less than 14 bits, hardcode remaining bytes.
            printh 00 00 00
            // SYSEX_END
            printh f7
            //
            // Background Shutdown
            // SYSEX_START DMID VERSION DEVICE
            printh f0 00 26 05 01 7f
            // PN_LSB PN_MSB
            printh 23 02 // Background Shutdown
            // TG_LSB
            printh 00 // Reserved
            // TG_MSB
            printh 00 // Reserved
            // VAL (4 bytes to be packed into 5 groups of 7 bit)
            // Bitfield. Bit 0: enable/disable background shutdown
            prints nBackOff.val,1
            // Bitfield uses less than 7 bits, hardcode remaining bytes.
            printh 00 00 00 00
            // SYSEX_END
            printh f7
            //
            // Dark Mode
            // SYSEX_START DMID VERSION DEVICE
            printh f0 00 26 05 01 7f
            // PN_LSB PN_MSB
            printh 24 02 // Background Shutdown
            // TG_LSB
            printh 00 // Reserved
            // TG_MSB
            printh 00 // Reserved
            // VAL (4 bytes to be packed into 5 groups of 7 bit)
            // 0: light mode, 1: dark mode
            prints btDark.val,1
            // Value uses less than 7 bits, hardcode remaining bytes.
            printh 00 00 00 00
            // SYSEX_END
            printh f7
        
Hotspot fLoadColors
    Attributes
        ID               : 44
        Scope            : local
        Send Component ID: disabled
    
    Events
        Touch Press Event
            // Changes all page components according to the current color set.
            // Note: This component has to be the last component of the page.
            //
            // sys0: component id
            // sys1: component type
            // sys2: picture id offset for current color mode
            sys2=Settings.colorMode.val*Settings.picCount.val
            for(sys0=0;sys0<fLoadColors.id;sys0++)
            {
                // More convenient for typing
                sys1=b[sys0].type
                if(sys1==121)
                {
                    // Types that have bco
                    // page
                    if(b[sys0].bco==Settings.backCol2Deflt.val)
                    {
                        b[sys0].bco=Settings.backCol2.val
                    }else
                    {
                        b[sys0].bco=Settings.backCol.val
                    }
                }else if(sys1==54||sys1==59||sys1==116||sys1==55)
                {
                    // Types that have bco, pco, and sta:
                    // number, float, text, scroll text
                    if(b[sys0].sta==1)
                    {
                        if(b[sys0].bco==Settings.backCol2Deflt.val)
                        {
                            b[sys0].bco=Settings.backCol2.val
                            b[sys0].pco=Settings.frontCol2.val
                        }else
                        {
                            b[sys0].bco=Settings.backCol.val
                            b[sys0].pco=Settings.frontCol.val
                        }
                    }
                }else if(sys1==98||sys1==53)
                {
                    // Types that have pco, pic, pco2, pic2, sta:
                    // button, dual-state button
                    if(b[sys0].sta==2)
                    {
                        b[sys0].pco=Settings.frontCol.val
                        b[sys0].pco2=Settings.backCol.val
                        b[sys0].pic=b[sys0].pic%Settings.picCount.val
                        b[sys0].pic+=sys2
                        b[sys0].pic2=b[sys0].pic+1
                    }
                }else if(sys1==58)
                {
                    // Types that have bco, pco
                    // QR Code
                    b[sys0].bco=Settings.backCol.val
                    b[sys0].pco=Settings.qrFrontCol.val
                }else if(sys1==1)
                {
                    // Types that have pic, pic1, pic2:
                    // slider
                    b[sys0].pic=b[sys0].pic%Settings.picCount.val
                    b[sys0].pic+=sys2
                    b[sys0].pic1=b[sys0].pic
                    b[sys0].pic2=b[sys0].pic2%Settings.picCount.val
                    b[sys0].pic2+=sys2
                }
            }
        
