Page Menu
    Attributes
        ID               : 0
        Scope            : local
        Send Component ID: disabled
        Locked           : no
    
    Events
        Preinitialize Event
            // Prevent page refreshs until postinit
            ref_stop
            //
            // Load colors
            click fLoadColors,1
            //
            // Display username
            if(User_Settings.userNum.val==0)
            {
                tUser.txt=" "+User_Settings.u0Name.txt
            }else if(User_Settings.userNum.val==1)
            {
                tUser.txt=" "+User_Settings.u1Name.txt
            }else if(User_Settings.userNum.val==2)
            {
                tUser.txt=" "+User_Settings.u2Name.txt
            }
            //
            // Display all active modes
            sys0=Settings.activeModes.val&1
            if(sys0!=0)
            {
                // Simple Mode is active
                tMSI.bco=6900
            }
            sys0=Settings.activeModes.val&2
            if(sys0!=0)
            {
                // MIDI Live Mode is active
                tMML.bco=6900
            }
            sys0=Settings.activeModes.val&4
            if(sys0!=0)
            {
                // Lightsaber Mode is active
                tMLS.bco=6900
            }
            // Settings are not available to user 0
            if(User_Settings.userNum.val==0)
            {
                vis bSettings,0
            }
            //
            // Load Help Mode
            if(Help_Info.helpMode.val==0)
            {
                vis qrHelp,0
                fill qrHelp.x,qrHelp.y,qrHelp.w,qrHelp.h,b[0].bco
            }else
            {
                tempStr.txt=tTitle.txt
                showHelp.val=1
                click 0,1
            }
            //
            // Enable page refreshs again
            ref_star
        
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal Operation
                if(Other_Settings.nBackOff.val!=0)
                {
                    // Default "stop" action.
                    // Send Mode Emergency Stop which turns all outputs off (but actually keeps the current mode)
                    prints "mes",3
                    // Set all ontime and duty settings of all pages to 0.
                    // First come the global variables then the local variables.
                    Simple.ontime.val=0
                    Simple.duty.val=0
                    Simple.set1.val=Simple.set1.val&0x0000ffff
                    Simple.set2.val=Simple.set2.val&0x0000ffff
                    Simple.set3.val=Simple.set3.val&0x0000ffff
                    Simple.set4.val=Simple.set4.val&0x0000ffff
                    Simple.set5.val=Simple.set5.val&0x0000ffff
                    Simple.set6.val=Simple.set6.val&0x0000ffff
                    MIDI_Live.ontime.val=0
                    MIDI_Live.duty.val=0
                    MIDI_Live.set1.val=0
                    MIDI_Live.set2.val=0
                    MIDI_Live.set3.val=0
                    MIDI_Live.set4.val=0
                    MIDI_Live.set5.val=0
                    MIDI_Live.set6.val=0
                    Lightsaber.ontime.val=0
                    Lightsaber.ontimes12.val=0
                    Lightsaber.ontimes34.val=0
                    Lightsaber.ontimes56.val=0
                }
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkf2c"
                click qrHelp,0
            }
        
Variable (string) tempStr
    Attributes
        ID            : 12
        Scope         : local
        Text          : 
        Max. Text Size: 32
    
Variable (int32) showHelp
    Attributes
        ID   : 13
        Scope: local
        Value: 0
    
Text tTitle
    Attributes
        ID                 : 1
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Menu
        Max. Text Size     : 22
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tUser
    Attributes
        ID                 : 7
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               :  Guest
        Max. Text Size     : 32
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMSI
    Attributes
        ID                 : 8
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Simple
        Max. Text Size     : 16
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMML
    Attributes
        ID                 : 9
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : MIDI Live
        Max. Text Size     : 16
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
Text tMLS
    Attributes
        ID                 : 10
        Scope              : local
        Send Component ID  : disabled
        Associated Keyboard: none
        Text               : Lightsaber
        Max. Text Size     : 16
    
    Events
        Touch Press Event
            // Make this object "transparent" for clicks and click background.
            click 0,1
        
QR Code qrHelp
    Attributes
        ID               : 14
        Scope            : local
        Send Component ID: disabled
        Text             : git.io/JkvOK
        Max. Text Size   : 16
    
    Events
        Touch Release Event
            if(showHelp.val==0)
            {
                showHelp.val=1
                tempStr.txt=tTitle.txt
                tTitle.txt="Get Help/Link for..."
                // keep QR code on top
                ref qrHelp
                // Show modified title before stopping screen refreshs.
                doevents
                ref_stop
            }else
            {
                showHelp.val=0
                tTitle.txt=tempStr.txt
                // keep QR code on top
                ref qrHelp
                ref_star
            }
        
Button bSimple
    Attributes
        ID               : 2
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : Simple
        Max. Text Size   : 10
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkf2R"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                page Simple
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button bMIDILive
    Attributes
        ID               : 3
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : MIDI Live
        Max. Text Size   : 10
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkf20"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal operation
                page MIDI_Live
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button bSettings
    Attributes
        ID               : 4
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : Settings
        Max. Text Size   : 10
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkf2z"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal operation
                page Settings
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button bSwitchUser
    Attributes
        ID               : 5
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : Switch User
        Max. Text Size   : 20
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal operation
                // Make this object "transparent" for clicks and click background.
                click 0,1
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkf2g"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal Operation
                page Identification
            }else
            {
                // Show Help
            }
        
Button bLightsaber
    Attributes
        ID               : 6
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : Lightsaber
        Max. Text Size   : 10
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkf2E"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal operation
                page Lightsaber
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Button bHelp
    Attributes
        ID               : 11
        Scope            : local
        Send Component ID: disabled
        State            : unpressed
        Text             : Help & Info
        Max. Text Size   : 20
    
    Events
        Touch Press Event
            if(showHelp.val==0)
            {
                // Normal operation
            }else
            {
                // Show Help
                qrHelp.txt="git.io/Jkf2u"
            }
        
        Touch Release Event
            if(showHelp.val==0)
            {
                // Normal operation
                page Help_Info
            }else
            {
                // Show Help
                click qrHelp,0
            }
        
Hotspot fLoadColors
    Attributes
        ID               : 15
        Scope            : local
        Send Component ID: disabled
    
    Events
        Touch Press Event
            // Changes all page components according to the current color set.
            // Note: This component has to be the last component of the page.
            //
            // sys0: component id
            // sys1: component type
            // sys2: picture id offset for current color mode
            sys2=Settings.colorMode.val*Settings.picCount.val
            for(sys0=0;sys0<fLoadColors.id;sys0++)
            {
                // More convenient for typing
                sys1=b[sys0].type
                if(sys1==121)
                {
                    // Types that have bco
                    // page
                    if(b[sys0].bco!=Settings.backCol2.val)
                    {
                        b[sys0].bco=Settings.backCol.val
                    }
                }else if(sys1==54||sys1==59||sys1==116||sys1==55)
                {
                    // Types that have bco, pco, and sta:
                    // number, float, text, scroll text
                    if(b[sys0].sta==1)
                    {
                        if(b[sys0].bco!=Settings.backCol2.val)
                        {
                            b[sys0].bco=Settings.backCol.val
                            b[sys0].pco=Settings.frontCol.val
                        }else
                        {
                            b[sys0].pco=Settings.frontCol2.val
                        }
                    }
                }else if(sys1==98||sys1==53)
                {
                    // Types that have pco, pic, pco2, pic2, sta:
                    // button, dual-state button
                    if(b[sys0].sta==2)
                    {
                        b[sys0].pco=Settings.frontCol.val
                        b[sys0].pco2=Settings.backCol.val
                        b[sys0].pic=b[sys0].pic%Settings.picCount.val
                        b[sys0].pic+=sys2
                        b[sys0].pic2=b[sys0].pic+1
                    }
                }else if(sys1==58)
                {
                    // Types that have bco, pco
                    // QR Code
                    b[sys0].bco=Settings.backCol.val
                    b[sys0].pco=Settings.qrFrontCol.val
                }else if(sys1==1)
                {
                    // Types that have pic, pic2:
                    // slider
                    b[sys0].pic=b[sys0].pic%Settings.picCount.val
                    b[sys0].pic+=sys2
                    b[sys0].pic2=b[sys0].pic2%Settings.picCount.val
                    b[sys0].pic2+=sys2
                }
            }
        
